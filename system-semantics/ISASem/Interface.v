Require Import Strings.String.

Require Import SSCCommon.Options.
Require Import SSCCommon.Common.

(** The architecture parameters that must be provided to the interface *)
Module Type Arch.

  (** The type of registers, most likely string, but may be more fancy *)
  Parameter reg : Type.

  (** We need to implement a gmap indexed by registers *)
  Parameter reg_eq : EqDecision reg.
  #[export] Existing Instance reg_eq.
  Parameter reg_countable : @Countable reg reg_eq.
  #[export] Existing Instance reg_countable.

  (** Register value type are dependent on the register, therefore we need all
      the dependent type manipulation typeclasses *)
  Parameter reg_type : reg → Type.
  Parameter reg_type_eq : ∀ (r : reg), EqDecision (reg_type r).
  #[export] Existing Instance reg_type_eq.
  Parameter reg_type_countable : ∀ (r : reg), Countable (reg_type r).
  #[export] Existing Instance reg_type_countable.
  Parameter reg_type_inhabited : ∀ r : reg, Inhabited (reg_type r).
  #[export] Existing Instance reg_type_inhabited.
  Parameter ctrans_reg_type : CTrans reg_type.
  #[export] Existing Instance ctrans_reg_type.
  Parameter ctrans_reg_type_simpl : CTransSimpl reg_type.
  #[export] Existing Instance ctrans_reg_type_simpl.
  Parameter reg_type_eq_dep_dec : EqDepDecision reg_type.
  #[export] Existing Instance reg_type_eq_dep_dec.


  (** Register access kind (architecture specific) *)
  Parameter reg_acc : Type.

  Parameter reg_acc_eq : EqDecision reg_acc.
  #[export] Existing Instance reg_acc_eq.


  Parameter CHERI : bool.
  Parameter cap_size : N.

(** Physical addresses type. Since models are expected to be architecture
      specific in this, there is no need for a generic way to extract a
      bitvector from it*)
  Parameter pa : Type.

  (** We need to implement a gmap indexed by pa *)
  Parameter pa_eq : EqDecision pa.
  #[export] Existing Instance pa_eq.
  Parameter pa_countable : @Countable pa pa_eq.
  #[export] Existing Instance pa_countable.

  (** Add an offset to a physical address. Can wrap if out of bounds *)
  Parameter pa_addZ : pa → Z → pa.

  (** This need to behave sensibly.
      For fancy words: pa_addZ need to be an action of the group Z on pa *)
  Parameter pa_addZ_assoc :
    ∀ pa z z', pa_addZ (pa_addZ pa z) z' = pa_addZ pa (z + z')%Z.
  Parameter pa_addZ_zero : ∀ pa, pa_addZ pa 0 = pa.
  #[export] Hint Rewrite pa_addZ_assoc : arch.
  #[export] Hint Rewrite pa_addZ_zero : arch.

  Parameter pa_diffN : pa → pa → option N.
  Parameter pa_diffN_addZ:
    ∀ pa pa' n, pa_diffN pa' pa = Some n → pa_addZ pa (Z.of_N n) = pa'.
  Parameter pa_diffN_existZ:
    ∀ pa pa' z, pa_addZ pa z = pa' → is_Some (pa_diffN pa' pa).
  Parameter pa_diffN_minimalZ:
    ∀ pa pa' n, pa_diffN pa' pa = Some n →
                ∀ z', pa_addZ pa z' = pa' → (z' < 0 ∨ (Z.of_N n) ≤ z')%Z.

  (** Memory access kind (architecture specific) *)
  Parameter mem_acc : Type.

  Parameter mem_acc_eq : EqDecision mem_acc.
  #[export] Existing Instance mem_acc_eq.

  (** Is this access an explicit access, e.g. whose access was explicitely
      required by the instruction. As minimima, this must be not an IFetch or a TTW
      access *)
  Parameter is_explicit : mem_acc → bool.
  (** Is this access an instruction fetch read *)
  Parameter is_ifetch : mem_acc → bool.
  (** Is this access a translation table walk *)
  Parameter is_ttw : mem_acc → bool.

  (** All the access type classifiers below are for explicit accesses.
      Therefore, they must all imply [is_explicit] *)

  (** Is this access relaxed, aka. no acquire or release strength *)
  Parameter is_relaxed : mem_acc → bool.
  (** Is this an acquire or a release access (Depending on whether this is a
      read or write *)
  Parameter is_rel_acq : mem_acc → bool.
  (** Is this a weak PC acquire (not ordered after write release *)
  Parameter is_acq_rcpc : mem_acc → bool.
  (** Is this a standalone access, aka. not part of an exclusive or RMW pair.
      This is based on the access type, so an unmatched exclusive load would not be
      "standalone" *)
  Parameter is_standalone : mem_acc → bool.
  (** Is this an exclusive access *)
  Parameter is_exclusive : mem_acc → bool.
  (** Is this part of an RMW instruction. Another RMW access to the same address
      in the same instruction is expected *)
  Parameter is_atomic_rmw : mem_acc → bool.


  (** Abort description. This represent physical memory aborts on memory
      accesses, for example when trying to access outside of physical memory
      range. Those aborts are generated by the model*)
  Parameter abort : Type.

  (** Barrier types *)
  Parameter barrier : Type.

  Parameter barrier_eq : EqDecision barrier.
  #[export] Existing Instance barrier_eq.


  (** Cache operations (data and instruction caches) *)
  Parameter cache_op : Type.

  Parameter cache_op_eq : EqDecision cache_op.
  #[export] Existing Instance cache_op_eq.

  (** TLB operations *)
  Parameter tlb_op : Type.

  Parameter tlb_op_eq : EqDecision tlb_op.
  #[export] Existing Instance tlb_op_eq.

  (** Fault type for a architectural fault or exception *)
  Parameter fault : Type.

  Parameter fault_eq : EqDecision fault.
  #[export] Existing Instance fault_eq.
End Arch.

Module Interface (A : Arch).

  Import A.
  #[local] Open Scope N.

  (** ** Memory utility *)
  (** Memory access kind *)
  Notation accessKind := mem_acc.

  Definition pa_addN pa n := pa_addZ pa (Z.of_N n).
  Lemma pa_addN_assoc pa n n':
    pa_addN (pa_addN pa n) n' = pa_addN pa (n + n').
  Proof. unfold pa_addN. rewrite pa_addZ_assoc. f_equal. lia. Qed.
  #[export] Hint Rewrite pa_addN_assoc : pa.
  Lemma pa_addN_zero pa : pa_addN pa 0 = pa.
  Proof. unfold pa_addN. apply pa_addZ_zero. Qed.
  #[export] Hint Rewrite pa_addN_zero : pa.
  Lemma pa_diffN_addN pa pa' n:
    pa_diffN pa' pa = Some n → pa_addN pa n = pa'.
  Proof. unfold pa_addN. apply pa_diffN_addZ. Qed.
  Hint Immediate pa_diffN_addN : pa.
  Lemma pa_diffN_existN pa pa' n:
    pa_addN pa n = pa' → is_Some (pa_diffN pa' pa).
  Proof. unfold pa_addN. apply pa_diffN_existZ. Qed.
  Lemma pa_diffN_minimalN pa pa' n:
    pa_diffN pa' pa = Some n → ∀ n', pa_addN pa n' = pa' → n ≤ n'.
  Proof. sauto use:pa_diffN_minimalZ. Qed.

  (* If faced with [pa_add pa n = pa_add pa n'], trying to prove [n = n'] is a good
     idea *)
  Definition f_equal_pa_addN pa := f_equal (pa_addN pa).
  Hint Resolve f_equal_pa_addN : pa.

  (** The list of all physical addresses accessed when accessing [pa] with size
      [n] *)
  Definition pa_range pa n := seqN 0 n |> map (λ n, pa_addN pa n).

  Lemma pa_range_length pa n : length (pa_range pa n) = N.to_nat n.
  Proof. unfold pa_range. by autorewrite with list. Qed.

  Definition pa_in_range pa size pa' : Prop :=
    is_Some $
      diff ← pa_diffN pa' pa;
    guard' (diff < size)%N.
  #[global] Instance pa_in_range_dec pa size pa' :
    Decision (pa_in_range pa size pa').
  Proof. unfold pa_in_range. tc_solve. Defined.

  Lemma pa_in_range_spec pa size pa':
    pa_in_range pa size pa' ↔ ∃ n, pa_addN pa n = pa' ∧ n < size.
  Proof.
    unfold pa_in_range, is_Some.
    split.
    - cdestruct |- ? #CDestrEqOpt.
      eauto with pa.
    - cdestruct |- ?.
      odestruct pa_diffN_existN; first eassumption.
      opose proof (pa_diffN_minimalN _ _ _ _ _ _); try eassumption.
      typeclasses eauto with core option lia.
  Qed.

  Definition pa_overlap pa1 size1 pa2 size2 : Prop :=
    pa_in_range pa1 size1 pa2 ∨ pa_in_range pa2 size2 pa1.
  #[global] Typeclasses Transparent pa_overlap.

  Lemma pa_overlap_spec pa1 size1 pa2 size2 :
    pa_overlap pa1 size1 pa2 size2 ∧ 0 < size1 ∧ 0 < size2 ↔
      ∃ n1 n2, (n1 < size1 ∧ n2 < size2 ∧ pa_addN pa1 n1 = pa_addN pa2 n2)%N.
  Proof.
    unfold pa_overlap.
    setoid_rewrite pa_in_range_spec.
    split.
    (* TODO broken *)
    - cdestruct pa1,pa2 |- ? # CDestrSplitGoal;
      setoid_rewrite pa_addN_assoc;
      typeclasses eauto with core lia pa.
    - cdestruct |- ** as n1 n2 H1 H2 H.
      intuition; try lia.
      destruct decide (n1 ≤ n2).
      1: right; exists (n2 - n1).
      2: left; exists (n1 - n2).
      (* TODO figure out better automation on pa_addZ *)
      all: intuition; try lia.
      all: unfold pa_addN in *.
      all: rewrite N2Z.inj_sub by lia.
      all: rewrite <- pa_addZ_assoc.
      all: (rewrite H || rewrite <- H).
      all: rewrite pa_addZ_assoc.
      all: rewrite <- pa_addZ_zero.
      all: f_equal; lia.
  Qed.

  Lemma pa_overlap_refl pa size :
    0 < size → pa_overlap pa size pa size.
  Proof.
    unfold pa_overlap. left.
    apply pa_in_range_spec.
    eexists.
    by rewrite pa_addN_zero.
  Qed.
  Hint Resolve pa_overlap_refl : pa.

  Lemma pa_overlap_sym pa1 size1 pa2 size2 :
    pa_overlap pa1 size1 pa2 size2 → pa_overlap pa2 size2 pa1 size1.
  Proof. unfold pa_overlap. tauto. Qed.
  Hint Immediate pa_overlap_sym : pa.

  Lemma pa_overlap_sym_iff pa1 size1 pa2 size2 :
    pa_overlap pa1 size1 pa2 size2 ↔ pa_overlap pa2 size2 pa1 size1.
  Proof. unfold pa_overlap. tauto. Qed.

  Module DepOn.
    Record t :=
      make
        {
          (** The list of registers the effect depends on. *)
          regs : list reg;
          (** The list of memory access the effect depends on. The number
              corresponds to the memory reads done by the instruction in the
              order specified by the instruction semantics. The indexing starts
              at 0. *)
          mem_reads : list N
        }.

    #[global] Instance eq_dec : EqDecision t.
    Proof. solve_decision. Defined.

  End DepOn.

  (** ** Memory read request *)
  Module ReadReq.
    #[local] Open Scope N.
    Record t {n : N} :=
      make
        { pa : pa;
          access_kind : accessKind;
          tag : bool;
          (** OLD: The address dependency. If unspecified, it can be interpreted as
            depending on all previous registers and memory values that were read
            *)
          addr_dep_on : option DepOn.t;
        }.
    Arguments t : clear implicits.

    #[global] Instance eq_dec n : EqDecision (t n).
    Proof. solve_decision. Defined.

    #[global] Instance jmeq_dec : EqDepDecision t.
    Proof. intros ? ? ? [] []. decide_jmeq. Defined.

  End ReadReq.

  (** ** Memory write request *)
  Module WriteReq.
    #[local] Open Scope N.
    Record t {n : N} :=
      make
        { pa : pa;
          access_kind : accessKind;
          value : bv (8 * n);
          tag : option bool;
          (** OLD: The address dependency. If unspecified, it can be interpreted as
            depending on all previous registers and memory values that were read
           *)
          addr_dep_on : option DepOn.t;
          (** OLD: The data dependency. If unspecified, it can be interpreted as
            depending on all previous registers and memory values that were read
            *)
          data_dep_on : option DepOn.t;
        }.
    Arguments t : clear implicits.

    #[global] Instance eq_dec n : EqDecision (t n).
    Proof. solve_decision. Defined.

    #[global] Instance jmeq_dec : EqDepDecision t.
    Proof. intros ? ? ? [] []. decide_jmeq. Defined.

  End WriteReq.

  Inductive outcome : Type -> Type :=
  | RegRead (reg : reg) : reg_acc -> outcome (reg_type reg)
  | RegWrite (reg : reg) : reg_acc -> (reg_type reg) -> option DepOn.t -> outcome unit
  | MemRead (n : N) : ReadReq.t n ->
                      outcome (bv (8 * n) * option bool + abort)
  | MemWrite (n : N) : WriteReq.t n -> outcome (bool + abort)
  | MemWriteAnnounce (n : N) : pa -> outcome unit
  (** The deps here specify the control dependency *)
  | BranchAnnounce (pa : pa) (dep_on : option DepOn.t) : outcome unit
  | Barrier : barrier -> outcome unit
  | CacheOp : cache_op -> outcome unit
  | TlbOp : tlb_op -> outcome unit
  | FaultAnnounce : fault -> outcome unit
  | EretAnnounce : outcome unit

  (** Bail out when something went wrong; this may be refined in the future *)
  | GenericFail (msg : string) : outcome False.

  (********** Monad instance **********)

  (** This is a naive but inefficient implementation of the instruction monad.
      It might be replaced by an more efficient version later. *)
  Inductive iMon {A : Type} :=
  | Ret : A -> iMon
  | Next {T : Type} : outcome T -> (T -> iMon) -> iMon.
  Arguments iMon : clear implicits.

  Global Instance iMon_mret_inst : MRet iMon := { mret a := Ret }.

  Fixpoint iMon_bind {a b : Type} (ma : iMon a) (f : a -> iMon b) :=
    match ma with
    | Ret x => f x
    | Next oc k => Next oc (fun x => iMon_bind (k x) f) end.
  Global Instance iMon_mbind_inst : MBind iMon :=
    { mbind _ _ f x := iMon_bind x f}.

  Fixpoint iMon_fmap {a b : Type} (ma : iMon a) (f : a -> b) :=
    match ma with
    | Ret x => Ret (f x)
    | Next oc k => Next oc (fun x => iMon_fmap (k x) f)
    end.
  Global Instance iMon_fmap_inst : FMap iMon :=
    { fmap _ _  f x := iMon_fmap x f}.

  (********** Instruction semantics and traces **********)

  (** The semantics of an complete instruction. A full definition of instruction
      semantics is allowed to have an internal state that gets passed from one
      instruction to the next. This is useful to handle pre-computed instruction
      semantics (e.g. Isla). For complete instruction semantics, we expect that
      A will be unit.*)
  Record iSem :=
    {
      (** The instruction model internal state *)
      isa_state : Type;
      (** The instruction model initial state for a thread with a specific Tid
       *)
      init_state : nat -> isa_state;
      semantic : isa_state -> iMon isa_state
    }.

  (** A single event in an instruction execution. As implied by the definition
      events cannot contain termination outcome (outcomes of type
      `outcome False`) *)
  Inductive iEvent :=
  | IEvent {T : Type} : outcome T -> T -> iEvent.


  (** Get the content of a barrier, returns none if not a barrier (or is an
        invalid EID) *)
  Definition get_barrier (ev : iEvent) : option barrier:=
    match ev with
    | IEvent (Barrier b) () => Some b
    | _ => None
    end.

  (** Get the physical address out of an memory event *)
  Definition get_pa (e : iEvent) : option pa:=
    match e with
    | IEvent (MemRead _ rr) _ => Some rr.(ReadReq.pa)
    | IEvent (MemWrite _ wr) _ => Some wr.(WriteReq.pa)
    | _ => None
    end.

 (** Get the size out of an memory event *)
  Definition get_size (ev : iEvent) : option N :=
    match ev with
    | IEvent (MemRead n _) _ => Some n
    | IEvent (MemWrite n _) _ => Some n
    | _ => None
    end.

  (** Get the value out of a memory event *)
  Definition get_mem_value (ev : iEvent) : option bvn :=
    match ev with
    | IEvent (MemRead n _) (inl (bv, _)) => Some (bv : bvn)
    | IEvent (MemWrite n wr) _ => Some (wr.(WriteReq.value) : bvn)
    | _ => None
    end.

  Lemma get_mem_value_size (ev : iEvent) bv :
    get_mem_value ev = Some bv → get_size ev = Some (bvn_n bv / 8)%N.
  Proof.
    destruct ev; destruct o;
      cdestruct bv |- ** #CDestrMatch; cbn; f_equal; lia.
  Qed.

  Definition get_access_kind (ev : iEvent) : option mem_acc :=
    match ev with
    | IEvent (MemRead _ rr) _ => Some rr.(ReadReq.access_kind)
    | IEvent (MemWrite _ wr) _ => Some wr.(WriteReq.access_kind)
    | _ => None
    end.

 (** ** Register reads ***)

  Section isReg.
    Context (P : ∀ r : reg, reg_acc → reg_type r → Prop).
    Implicit Type ev : iEvent.

    Definition is_reg_readP ev : Prop :=
      match ev with
      | IEvent (RegRead reg racc) rval => P reg racc rval
      | _ => False
      end.
    #[export] Typeclasses Opaque is_reg_readP.
    Definition is_reg_readP_spec ev :
      is_reg_readP ev ↔
        ∃ reg racc rval, ev = IEvent (RegRead reg racc) rval ∧ P reg racc rval.
    Proof. destruct ev as [? []]; split; cdestruct |- **;naive_solver. Qed.
    Definition is_reg_readP_cdestr ev := cdestr_simpl false (is_reg_readP_spec ev).
    #[global] Existing Instance is_reg_readP_cdestr.

    Context `{Pdec: ∀ reg racc rval, Decision (P reg racc rval)}.
    #[global] Instance is_reg_readP_dec ev: Decision (is_reg_readP ev).
    Proof using Pdec. destruct ev. destruct o; cbn in *; tc_solve. Defined.

    (** ** Register writes *)
    Definition is_reg_writeP ev : Prop :=
      match ev with
      | IEvent (RegWrite reg racc rval _) _ => P reg racc rval
      | _ => False
      end.

    Definition is_reg_writeP_spec ev :
      is_reg_writeP ev ↔
        ∃ reg racc rval rdep,
          ev = IEvent (RegWrite reg racc rval rdep) () ∧ P reg racc rval.
    Proof.
      destruct ev as [? []];
        split; cdestruct |- ?; destruct t;naive_solver.
    Qed.
    Definition is_reg_writeP_cdestr ev := cdestr_simpl false (is_reg_writeP_spec ev).
    #[global] Existing Instance is_reg_writeP_cdestr.

    #[global] Instance is_reg_writeP_dec ev: Decision (is_reg_writeP ev).
    Proof using Pdec. destruct ev as [? []]; cbn in *; tc_solve. Defined.

  End isReg.
  Notation is_reg_read := (is_reg_readP (λ _ _ _, True)).
  Notation is_reg_write := (is_reg_writeP (λ _ _ _, True)).

  (** ** Memory reads *)

  (** *** Memory reads request

      This is the general case for both failed and successful memory reads *)
  Section isMemReadReq.
    Context
      (P : ∀ n : N, ReadReq.t n → (bv (8 * n) * option bool + abort) → Prop).
    Implicit Type ev : iEvent.

    Definition is_mem_read_reqP ev : Prop :=
      match ev with
      | IEvent (MemRead n rr) rres => P n rr rres
      | _ => False
      end.
    #[export] Typeclasses Opaque is_mem_read_reqP.

    Definition is_mem_read_reqP_spec ev:
      is_mem_read_reqP ev ↔ ∃ n rr rres, ev = IEvent (MemRead n rr) rres ∧ P n rr rres.
    Proof. destruct ev; destruct o;split; cdestruct |- ?; destruct t0; naive_solver. Qed.

    Definition is_mem_read_reqP_cdestr ev := cdestr_simpl false (is_mem_read_reqP_spec ev).
    #[global] Existing Instance is_mem_read_reqP_cdestr.

    Context `{Pdec : ∀ n rr rres, Decision (P n rr rres)}.
    #[global] Instance is_mem_read_reqP_dec ev : Decision (is_mem_read_reqP ev).
    Proof using Pdec. destruct ev; destruct o; cbn in *; tc_solve. Defined.
  End isMemReadReq.
  Notation is_mem_read_req := (is_mem_read_reqP (λ _ _ _, True)).

  (** *** Successful memory reads *)
  Section IsMemRead.
    Context (P : ∀ n : N, ReadReq.t n → bv (8 * n) → option bool → Prop).
    Implicit Type ev : iEvent.

    (** Filters memory read that are successful (that did not get a physical
        memory abort *)
    Definition is_mem_readP ev : Prop :=
      is_mem_read_reqP (λ n rr rres,
                          match rres with
                          | inl (rval, otag) => P n rr rval otag
                          | _ => False end) ev.
    #[export] Typeclasses Opaque is_mem_readP.

    Definition is_mem_readP_spec ev:
      is_mem_readP ev ↔ ∃ n rr rval otag, ev = IEvent (MemRead n rr) (inl (rval, otag)) ∧ P n rr rval otag.
    Proof. unfold is_mem_readP. rewrite is_mem_read_reqP_spec. hauto l:on. Qed.
    Definition is_mem_readP_cdestr ev := cdestr_simpl false (is_mem_readP_spec ev).
    #[global] Existing Instance is_mem_readP_cdestr.

    Context `{Pdec: ∀ n rr rval otag, Decision (P n rr rval otag)}.
    #[global] Instance is_mem_readP_dec ev: Decision (is_mem_readP ev).
    Proof using Pdec. unfold is_mem_readP. solve_decision. Defined.
  End IsMemRead.
  Notation is_mem_read := (is_mem_readP (λ _ _ _ _, True)).

  Section isMemWriteReq.
    Context
      (P : ∀ n : N, WriteReq.t n → (bool + abort) → Prop).
    Implicit Type ev : iEvent.

    Definition is_mem_write_reqP ev : Prop :=
      match ev with
      | IEvent (MemWrite n wr) wres => P n wr wres
      | _ => False
      end.
    Typeclasses Opaque is_mem_write_reqP.

    Definition is_mem_write_reqP_spec ev:
      is_mem_write_reqP ev ↔ ∃ n wr wres, ev = IEvent (MemWrite n wr) wres ∧ P n wr wres.
    Proof. destruct ev; destruct o;split; cdestruct |- ?; destruct t0; naive_solver. Qed.
    Definition is_mem_write_reqP_cdestr ev := cdestr_simpl false (is_mem_write_reqP_spec ev).
    #[global] Existing Instance is_mem_write_reqP_cdestr.

    Context `{Pdec: ∀ n wr wres, Decision (P n wr wres)}.
    #[global] Instance is_mem_write_reqP_dec ev: Decision (is_mem_write_reqP ev).
    Proof using Pdec. destruct ev; destruct o; cbn in *; tc_solve. Defined.
  End isMemWriteReq.
  Notation is_mem_write_req := (is_mem_write_reqP (λ _ _ _, True)).

  (** *** Successful memory writes *)
  Section isMemWrite.
    Context
      (P : ∀ n : N, WriteReq.t n → Prop).
    Implicit Type ev : iEvent.

    (** Filters memory writes that are successful (that did not get a physical
        memory abort, or an exclusive failure).*)
    Definition is_mem_writeP ev: Prop :=
      is_mem_write_reqP (λ n wr wres,
                           match wres with
                           | inl true => P n wr
                           | _ => False end) ev.
    Typeclasses Opaque is_mem_writeP.

    Definition is_mem_writeP_spec ev:
      is_mem_writeP ev ↔
      ∃ n wr, ev = IEvent (MemWrite n wr) (inl true) ∧ P n wr.
    Proof. unfold is_mem_writeP. rewrite is_mem_write_reqP_spec. hauto l:on. Qed.
    Definition is_mem_writeP_cdestr ev := cdestr_simpl false (is_mem_writeP_spec ev).
    #[global] Existing Instance is_mem_writeP_cdestr.

    Context `{Pdec: ∀ n wr, Decision (P n wr)}.
    #[global] Instance is_mem_writeP_dec ev: Decision (is_mem_writeP ev).
    Proof using Pdec. unfold is_mem_writeP. solve_decision. Defined.
  End isMemWrite.
  Notation is_mem_write := (is_mem_writeP (λ _ _, True)).


  Definition is_mem_event_req (ev : iEvent) :=
    is_mem_read_req ev \/ is_mem_write_req ev.
  #[global] Typeclasses Transparent is_mem_event_req.

  Definition is_mem_event (ev : iEvent) :=
    is_mem_read ev \/ is_mem_write ev.
  #[global] Typeclasses Transparent is_mem_event.
  (** ** Allow filtering memory events by kind more easily *)
  Section MemEventByKind.
    Context (P : accessKind → Prop).
    Context {Pdec : ∀ acc, Decision (P acc)}.
    Implicit Type ev : iEvent.

    Definition is_mem_read_kindP :=
      is_mem_readP (λ _ rr _ _, P rr.(ReadReq.access_kind)).
    #[global] Typeclasses Transparent is_mem_read_kindP.
    Definition is_mem_write_kindP :=
      is_mem_writeP (λ _ wr, P wr.(WriteReq.access_kind)).
    #[global] Typeclasses Transparent is_mem_write_kindP.

    Definition is_mem_event_kindP (ev : iEvent) :=
      if get_access_kind ev is Some acc then P acc else False.
    #[global] Instance is_mem_event_kindP_dec ev:
      Decision (is_mem_event_kindP ev).
    Proof using Pdec. unfold is_mem_event_kindP. tc_solve. Defined.
  End MemEventByKind.


  (** ** Barriers *)
  Section isBarrier.
    Context (P : barrier → Prop).
    Implicit Type ev : iEvent.

    Definition is_barrierP ev: Prop :=
      if ev is IEvent (Barrier b) _ then P b else False.
    Typeclasses Opaque is_barrierP.

    Definition is_barrierP_spec ev:
      is_barrierP ev ↔ ∃ barrier, ev = IEvent (Barrier barrier) () ∧ P barrier.
    Proof. destruct ev; destruct o; split; cdestruct |- ?; destruct t; naive_solver. Qed.

    Context `{Pdec: ∀ b, Decision (P b)}.
    #[global] Instance is_barrierP_dec ev: Decision (is_barrierP ev).
    Proof using Pdec. unfold_decide. Defined.
  End isBarrier.
  Notation is_barrier := (is_barrierP (λ _, True)).


  (** An execution trace for a single instruction.
      If the result is an A, it means a successful execution that returned A
      If the result is a string, it means a GenericFail *)
  Definition iTrace (A : Type) : Type := list iEvent * (A + string).

  Arguments outcome : clear implicits.
  Arguments iMon : clear implicits.
  Arguments iSem : clear implicits.
  Arguments iTrace : clear implicits.
  Arguments iEvent : clear implicits.

End Interface.

Module Type InterfaceT (A : Arch).
  Include Interface A.
End InterfaceT.

Module Type InterfaceWithArch.
  Declare Module Arch : Arch.
  Declare Module Interface : InterfaceT Arch.
End InterfaceWithArch.
